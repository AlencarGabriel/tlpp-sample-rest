#include "tlpp-core.th"

#define _dbDrive		'SQLITE_SYS'
#define	_AliasUSR		"USERS"

static __nAlias  := 1

/*
NOTA:
Por ser um código-fonte de exemplo, não iremos utilizar nenhum recurso de encriptação
pois o intuito desse exemplo é ensinar os Verbos e funcionalidades básicas de maior
uso do REST do tlppCore
*/


//-------------------------------------------------------------------
/**
@function sampleCRUDtlppRestgenerateTables
@desc     Main function para gerar base e popular tabela de usuários para exemplos
          utilizados nesse exemplo de uso do REST
@author   TOTVS-TECH-TLPPCORE
@since    06/2019
*/
//-------------------------------------------------------------------
main function sampleCRUDtlppRestgenerateTables()

	local aDbTbl 		:= {}
	local cAliasTbl	:= ''
	local cAlias		:= ''

	conout( '', '' )
	conout( '---------' )
	conout( 'TLPP REST' )
	conout( '---------' )
	conout( '' )
	conout( date(), time() )
	conout( '' )
	conout( '... starting tables for workshop' )
	conout( '' )

	aDbTbl  :=	{ 									        ;
                { 'ID'			, 'C', 20	,0}	,;
                { 'NAME'		, 'C', 150,0}	,;
                { 'NICKNAME', 'C', 50	,0} ,;
                { 'USER'		, 'C', 150,0}	,;
                { 'PASSWORD', 'C', 150,0}	;
              }
	cAliasTbl := _AliasUSR
	dbCreate( cAliasTbl, aDbTbl, _dbDrive )

	// Cria indice de busca
		cAlias := NextAlias()
		dbUseArea( .F., _dbDrive, _AliasUSR, (cAlias), .F., .F. )
			(cAlias)->( dbCreateIndex(_AliasUSR+'01', 'ID'  , { || 'ID'   }, .T. ) )
			(cAlias)->( dbCreateIndex(_AliasUSR+'02', 'USER', { || 'USER' }, .T. ) )
		(cAlias)->( dbCloseArea() )
	aDbTbl 		:= {}
	cAliasTbl	:= ''
	conout( '... table [' + _AliasUSR +'] created' )
	conout( '' )

	initUsers()
	conout( '... table [' + _AliasUSR +'] initialized' )
	conout( '' )

	conout( '... end process for workshop' )
return


//-------------------------------------------------------------------
/**
@function initUsers
@desc     popula tabela de usuários
@author   TOTVS-TECH-TLPPCORE
@since    06/2019
*/
//-------------------------------------------------------------------
static function initUsers()

	local cAlias	:= NextAlias()
	local nOrder	:= 0
	local cID		  := ''
	local aUsers	:= {}
	local nA		  := 0

	// lista de usuarios iniciais
		aadd( aUsers, {'Joao dos Santos','joaozinho','joao' ,'senha@123'} )
		aadd( aUsers, {'Maria Silva'    ,'marie'	  ,'maria','senha@123'} )
		aadd( aUsers, {'Jose Goncalves' ,'ze'		    ,'jose' ,'senha@123'} )
		aadd( aUsers, {'Julia Esteves'  ,''			    ,'julia','senha@123'} )

	// Grava na tabela
		dbUseArea( .T., _dbDrive, _AliasUSR, cAlias, .T., .F. )
			dbSelectArea( cAlias )
			dbSetIndex( _AliasUSR+'01' )
			nOrder := OrdNumber( _AliasUSR+'01' )
			dbSetOrder(nOrder)

			for nA := 1 to len( aUsers )

				cID := DtoS( date() ) + strtran( time(), ':', "" ) + strzero(nA,3)
					if ( !dbSeek(cID) )

						dbAppend( .T. )
							( cAlias )->ID			  := cID
							( cAlias )->NAME		  := aUsers[nA][1]
							( cAlias )->NICKNAME	:= aUsers[nA][2]
							( cAlias )->USER		  := aUsers[nA][3]
							( cAlias )->PASSWORD	:= aUsers[nA][4]
						dbCommit()
						dbRUnLock( Recno() )

					endif

			next
		( cAlias )->( dbCloseArea() )
return


//-------------------------------------------------------------------
/**
@function sampleCRUDtlppRestalteraUserByID
@desc     Altera um usuário com base no ID
@author   TOTVS-TECH-TLPPCORE
@since    06/2019
@param    cID CR ID
@param    cPass CR Senha
@param    cName CR Nome
@param    cNick CR Apelido
@return   L lOK TRUE para ação com sucesso
*/
//-------------------------------------------------------------------
function sampleCRUDtlppRestalteraUserByID( cID, cPass, cName, cNick )
	lOK := sampleCRUDtlppRestalteraUser( .T., cID, cPass, cName, cNick )
return lOK

//-------------------------------------------------------------------
/**
@function sampleCRUDtlppRestalteraUserByUser
@desc     Altera um usuário com base no usuário
@author   TOTVS-TECH-TLPPCORE
@since    06/2019
@param    cUser CR Usuário
@param    cPass CR Senha
@param    cName CR Nome
@param    cNick CR Apelido
@return   L lOK TRUE para ação com sucesso
*/
//-------------------------------------------------------------------
function sampleCRUDtlppRestalteraUserByUser( cUser, cPass, cName, cNick )
	lOK := sampleCRUDtlppRestalteraUser( .F., cUser, cPass, cName, cNick )
return lOK

//-------------------------------------------------------------------
/**
@function sampleCRUDtlppRestalteraUser
@desc     Altera um usuário
@author   TOTVS-TECH-TLPPCORE
@since    06/2019
@param    lID LR TRUE indica que Key é valor do ID, FALSE se refere ao usuário
@param    cKey CR Key
@param    cPass CR Senha
@param    cName CR Nome
@param    cNick CR Apelido
@return   L lOK TRUE para ação com sucesso
*/
//-------------------------------------------------------------------
static function sampleCRUDtlppRestalteraUser( lID, cKey, cPass, cName, cNick )

  local lOK		  := .F.
	local cQuery	:= ''
	local lParam	:= .F.

	if ( lID )
		cKey := padr( cKey, 20 )
	else
		cKey := padr( cKey, 150 )
	endif

	cQuery += 'update ' + _AliasUSR
	cQuery += ' set'
	if ( !empty(cPass) )
		cQuery += " PASSWORD = '" + cPass + "' "
		lParam := .T.
	endif
	if ( !empty(cName) )
		if ( lParam )
			cQuery += " ,"
		endif
		cQuery += " NAME = '" + padr( cName, 50 ) + "' "
		lParam := .T.
	endif
	if ( !empty(cNick) )
		if ( lParam )
			cQuery += " ,"
		endif
		cQuery += " NICKNAME = '" + padr( cNick, 50 ) + "' "
		lParam := .T.
	endif
	cQuery += ' where '
	if ( lID )
		cQuery += " ID = '" + cKey + "'"
	else
		cQuery += " USER = '" + cKey + "'"
	endif
	cQuery += " ;"

	lOK := dbSqlExec( "", cQuery, _dbDrive )
return lOK


//-------------------------------------------------------------------
/**
@function sampleCRUDtlppRestinsertUser
@desc     Insere um usuário na BD
@author   TOTVS-TECH-TLPPCORE
@since    06/2019
@param    cUser CR Usuário
@param    cPass CR Senha
@param    cName CR Nome
@param    cNick CR Apelido
@return   L lOK TRUE para ação com sucesso
*/
//-------------------------------------------------------------------
function sampleCRUDtlppRestinsertUser( cUser, cPass, cName, cNick )	

  local lOK 		:= .F.
	local cAlias	:= ''
	local cQuery	:= ''
	local lFound	:= .F.
	local cID		  := ''
	local nOrder	:= 0

	cAlias := NextAlias()
	cQuery := "select * from " + _AliasUSR
	cQuery += " where USER = '" + padr(cUser,150) + "';"
	dbSqlExec( cAlias, cQuery, _dbDrive )
	dbSelectArea( cAlias )
		lFound := (cAlias)->( !eof() )
	(cAlias)->( dbCloseArea() )

	if ( !lFound )

		cAlias := NextAlias()
		dbUseArea( .T., _dbDrive, _AliasUSR, cAlias, .T., .F. )
		dbSelectArea( cAlias )
		dbSetIndex( _AliasUSR+'01' )
		nOrder := OrdNumber( _AliasUSR+'01' )
		dbSetOrder(nOrder)

		cID := DtoS( date() ) + strtran( time(), ':', "" ) + CVALTOCHAR( ThreadID() )
			if ( !dbSeek(cID) )

				dbAppend( .T. )
					( cAlias )->ID			  := cID
					( cAlias )->NAME		  := cName
					( cAlias )->NICKNAME	:= cNick
					( cAlias )->USER		  := cUser
					( cAlias )->PASSWORD	:= cPass
				dbCommit()
				dbRUnLock( Recno() )

				lOK := .T.
			else
				lOK := .F.
				conout('invalid ID')
			endif
	else
		lOK := .F.
		conout('user already exists')
	endif

return  lOK


//-------------------------------------------------------------------
/**
@function sampleCRUDtlppRestgetUser
@desc     Retorna dados de um usuário
@author   TOTVS-TECH-TLPPCORE
@since    06/2019
@param    cUser CR Usuário
@return   A aUser Dados do usuário
*/
//-------------------------------------------------------------------
function sampleCRUDtlppRestgetUser( cUser )

	local aUser		:= {}
	local cAlias	:= ''
	local cQuery	:= ''

	cAlias := NextAlias()
	cQuery := "select * from " + _AliasUSR
	cQuery += " where USER = '" + padr(cUser,150) + "';"
	dbSqlExec( cAlias, cQuery, _dbDrive )
	dbSelectArea( cAlias )

		if ( (cAlias)->( !eof() ) )
			aadd( aUser, alltrim( (cAlias)->ID )		    )
			aadd( aUser, alltrim( (cAlias)->NAME )		  )
			aadd( aUser, alltrim( (cAlias)->NICKNAME )	)
			aadd( aUser, alltrim( (cAlias)->USER )		  )
		endif

	(cAlias)->( dbCloseArea() )

return aUser


//-------------------------------------------------------------------
/**
@function sampleCRUDtlppRestdeleteUser
@desc     Deleta usuário da base de dados
@author   TOTVS-TECH-TLPPCORE
@since    06/2019
@param    cUser CR Usuário
@return   L lOK TRUE para ação com sucesso
*/
//-------------------------------------------------------------------
function sampleCRUDtlppRestdeleteUser( cUser )

	local lOK 		:= .F.
	local cAlias	:= ''
	local cQuery	:= ''
	local lFound	:= .F.
	local cID		  := ''

	cAlias := NextAlias()
	cQuery := "select * from " + _AliasUSR
	cQuery += " where USER = '" + padr(cUser,150) + "';"

	dbSqlExec( cAlias, cQuery, _dbDrive )
	dbSelectArea( cAlias )
		lFound 	:= (cAlias)->( !eof() )
		cID		:= (cAlias)->ID
	(cAlias)->( dbCloseArea() )

	if ( lFound )

		cQuery += 'delete from ' + _AliasUSR
		cQuery += " where ID = '" + cID + "';"

		lOK := dbSqlExec( "", cQuery, _dbDrive )

	else
		lOK := .F.
		conout('user not found')
	endif

return lOK


//-------------------------------------------------------------------
/**
@function sampleCRUDtlppRestUserValid
@desc     Verifica usuário e senha na tabela USERS para verificar se tem acesso
@author   TOTVS-TECH-TLPPCORE
@since    06/2019
@param    cUser CR Usuário
@param    cPass CR Senha
@return   L lOK TRUE para dados de usuário e senha corretos
*/
//-------------------------------------------------------------------
function sampleCRUDtlppRestUserValid( cUser, cPass )

	local lLogon := .F.
	local cAlias	:= ''
	local cQuery	:= ''
	local lFound	:= .F.

	cAlias := NextAlias()
	cQuery := "select * from " + _AliasUSR
	cQuery += " where USER = '" + padr(cUser,150) + "';"

	dbSqlExec( cAlias, cQuery, _dbDrive )
	dbSelectArea( cAlias )
		if ( lFound := (cAlias)->( !eof() ) )
			cPassBD	:= alltrim( (cAlias)->PASSWORD )
		endif
	(cAlias)->( dbCloseArea() )

	if ( lFound )
		cPass := alltrim( cPass )
		if ( !(lLogon := (cPass == cPassBD)) )
			conout('invalid password')
		endif
	else
		lLogon := .F.
		conout('invalid user')
	endif

return lLogon


//-------------------------------------------------------------------
/**
@function NextAlias
@desc     Retorna próximo nome de Alias para ser usado no acesso ao BD
@author   TOTVS-TECH-TLPPCORE
@since    06/2019
@return   C cRet Alias a ser usado
*/
//-------------------------------------------------------------------
static function NextAlias()
	local cRet := "SCGN" + strzero(__nAlias,6,0)
	__nAlias++
	if ( __nAlias > 999999 )
		__nAlias := 1
	endif
return cRet
