#include "tlpp-core.th"
#include "tlpp-rest.th"

/*------------------------------------------------------------------
Altera dados completos de um usuário
Parâmetros via [raw body]
------------------------------------------------------------------*/
@Put("sample/rest/crud/body")
function putBodyUser()

  local cID   := ''
  local cUser := ''
  local cPass := ''
  local cName := ''
  local cNick := ''
  local jBody := ''

  jBody := JsonObject():new()
  jBody:fromJson( oRest:GetBodyRequest() )

  if ( jBody <> Nil )
    cID   := jBody:GetJsonText("id"      )
    if ( cID == 'null' )
      cID := Nil
    endif
    cUser := jBody:GetJsonText("user"    )
    if ( cUser == 'null' )
      cUser := Nil
    endif
    cPass := jBody:GetJsonText("password")
    cName := jBody:GetJsonText("name"    )
    cNick := jBody:GetJsonText("nick"    )
  endif

  processPutUser( cID, cUser, cPass, cName, cNick )
return 

/*------------------------------------------------------------------
Altera dados completos de um usuário
Parâmetros via [query]
------------------------------------------------------------------*/
@Put("sample/rest/crud/query")
function putQueryUser()

  local cID     := ''
  local cUser   := ''
  local cPass   := ''
  local cName   := ''
  local cNick   := ''
  local jQuery  := jSonObject():New()

  jQuery := oRest:getQueryRequest()

  if ( jQuery <> Nil )
    cID   := jQuery[ 'id'       ]
    cUser := jQuery[ 'user'     ]
    cPass := jQuery[ 'password' ]
    cName := jQuery[ 'name'     ]
    cNick := jQuery[ 'nick'     ]
  endif

  processPutUser( cID, cUser, cPass, cName, cNick )
return

/*-----------------------------------------------------------------
Altera dados completos de um usuário
Parâmetros via [pathstring]
------------------------------------------------------------------*/
@Put("sample/rest/crud/path/:user/:password/:name/:nick")
function putPathUser()

  local cID   := ''
  local cUser := ''
  local cPass := ''
  local cName := ''
  local cNick := ''
  local jPath := jSonObject():New()

  jPath := oRest:getPathParamsRequest()
  if ( jPath <> Nil )
    cID   := jPath[ 'id'        ]
    cUser := jPath[ 'user'      ]
    cPass := jPath[ 'password'  ]
    cName := jPath[ 'name'      ]
    cNick := jPath[ 'nick'      ]
  endif

  processPutUser( cID, cUser, cPass, cName, cNick )
return 

// ------------------------------------------------------------------ //
// ------------------------------------------------------------------ //
static function processPutUser( cID, cUser, cPass, cName, cNick )

  local cReturn := ''
  local lOK     := .T.
  local cMsg    := ''

  // Valida campo chave de busca
    if ( cID == Nil .or. empty(cID) )
      cID   := ''
      lOK   := .F.
      cMsg  := 'id'
    endif
    if ( !lOK )
      if ( cUser == Nil .or. empty(cUser) )
        cUser := ''
        lOK   := .F.
        if ( !empty(cMsg) )
          cMsg += ' | '
        endif
        cMsg  += 'user'
      else
        lOK   := .T.
        cMsg  := ''
      endif
    endif
    if ( !empty(cMsg) )
      cMsg := 'invalid parameter: ' + cMsg
    endif

  // Valida campos elegíveis para atualização
    if ( lOK )
      if ( cPass == Nil )
        cPass := ''
      endif
      if ( cName == Nil )
        cName := ''
      endif
      if ( cNick == Nil )
        cNick := ''
      endif
      if ( empty(cPass) .and. empty(cName) .and. empty(cNick) )
        lOK   := .F.
        cMsg  := 'no data were reported for change'
      endif
    endif

  if ( lOK )
    if ( cID <> Nil .and. !empty(cID) )
      lOK := sampleCRUDtlppRestalteraUserByID( cID, cPass, cName, cNick )
    else
      lOK := sampleCRUDtlppRestalteraUserByUser( cUser, cPass, cName, cNick )
    endif
    if ( !lOK )
      cMsg := 'update not executed !'
    else
      cMsg := 'successfully updated !' 
    endif
  endif

  cReturn := '{ "SUCESS": "' + if(lOK,'yes','no') + '", "MESSAGE": "' + cMsg + '" }'

  oRest:setResponse( cReturn )
return
