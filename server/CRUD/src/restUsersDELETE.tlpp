#include "tlpp-core.th"
#include "tlpp-rest.th"

/*------------------------------------------------------------------
deleta um usuário
Parêmetros via [raw body]
------------------------------------------------------------------*/
@Delete("sample/rest/crud/body")
function deleteBodyUser()

  local cUser := ''
  local jBody := ''

  jBody := JsonObject():new()
  jBody:fromJson( oRest:GetBodyRequest() )

  if ( jBody <> Nil )
    cUser := jBody:GetJsonText("user")
  endif

  processDeleteUser(cUser)
return


/*----------------------------------------------------------------
deleta um usuário
Parêmetros via [querystring]
----------------------------------------------------------------*/
@Delete("sample/rest/crud/query")
function deleteQueryUser()

  local cUser   := ''
  local jQuery  := jsonObject():New() 

  jQuery := oRest:GetQueryRequest()
  if ( jQuery <> Nil )
    cUser := jQuery['user']
  endif

  processDeleteUser(cUser)
return

/*
------------------------------------------------------------------
deleta um usuário
Parêmetros via [pathstring]
------------------------------------------------------------------
*/
@Delete("sample/rest/crud/path/:user")
function deletePathUser()

  local cUser := ''
  local jPath := jsonObject():New()

  jPath := oRest:GetPathParamsRequest()

  if ( jPath <> Nil )
    cUser := jPath[ 'user' ]
  endif

  processDeleteUser(cUser)
return


// ------------------------------------------------------------------ //
// ------------------------------------------------------------------ //
static function processDeleteUser(cUser)

  local cReturn := ''
  local lOK     := .T.
  local cMsg    := ''

  // Valida campo chave de busca
    if ( cUser == Nil .or. empty(cUser) )
      cUser := ''
      lOK   := .F.
      cMsg  := 'invalid parameter: user'
    endif

  if ( lOK )
    if ( !(lOK := sampleCRUDtlppRestdeleteUser( cUser )) )
      cMsg := 'could not deleted'
    else
      cMsg := 'record deleted'
    endif
  endif

  cReturn := '{ "SUCESS": "' + if(lOK,'yes','no') + '", "MESSAGE": "' + cMsg + '" }'

  oRest:SetResponse( cReturn )
return 
